apply plugin: 'signing'
apply plugin: 'maven-publish'

group = 'me.tatarka.yesdata'
version = '1.0.0'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        lib(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Yesdata'
                description = 'Errorprone check to verify you have implemented data classes correctly.'
                url = 'https://github.com/willowtreeapps/yesdata'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'evant'
                        name = 'Evan Tatarka'
                    }
                }
                scm {
                    connection = 'https://github.com/willowtreeapps/yesdata.git'
                    developerConnection = 'https://github.com/willowtreeapps/yesdata.git'
                    url = 'https://github.com/willowtreeapps/yesdata'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username project.findProperty('sonatype.username')
                password project.findProperty('sonatype.password')
            }
        }
    }
}

signing {
    sign publishing.publications.lib
}